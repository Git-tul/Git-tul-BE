// build.gradle (루트)
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

java {
    toolchain { // 로컬 환경과 무관한 빌드 환경 제공
        languageVersion = JavaLanguageVersion.of(17)
    }
}

bootJar {
    enabled = false
}

allprojects {
    group = 'io.gittul'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        // spring
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }
}

project(':app') {
    dependencies {
        implementation project(':core')
        implementation project(':infra')
    }
}

project('infra') {
    dependencies {
        implementation project(':core')
    }
}
